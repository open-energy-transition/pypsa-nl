# SPDX-FileCopyrightText: Contributors to Open-TYNDP <https://github.com/open-energy-transition/open-tyndp>
#
# SPDX-License-Identifier: MIT


from scripts._helpers import safe_pyear, find_free_port
from shutil import unpack_archive, copy2

# Retrieve
##########


if (PECD_DATASET := dataset_version("tyndp_pecd"))["source"] in ARCHIVE_SOURCES:

    rule retrieve_tyndp_pecd:
        input:
            zip_file=storage(
                PECD_DATASET["url"] + f"PECD_{PECD_DATASET['version']}.zip"
            ),
        output:
            dir=directory(PECD_DATASET["folder"]),
        log:
            "logs/retrieve_tyndp_pecd.log",
        run:
            copy2(input["zip_file"], output["dir"] + ".zip")
            unpack_archive(output["dir"] + ".zip", output["dir"])
            os.remove(output["dir"] + ".zip")


if (VIS_PLFM_DATASET := dataset_version("tyndp_vis_plfm"))["source"] in ARCHIVE_SOURCES:

    rule retrieve_tyndp_vp_data:
        input:
            zip_file=storage(VIS_PLFM_DATASET["url"]),
        output:
            dir=directory(VIS_PLFM_DATASET["folder"]),
            elec_demand=f"{VIS_PLFM_DATASET['folder']}/250117_TYNDP2024Scenarios_Electricity_Demand.xlsx",
            elec_flex=f"{VIS_PLFM_DATASET['folder']}/250117_TYNDP2024Scenarios_Electricity_Flexibility.xlsx",
            elec_supply=f"{VIS_PLFM_DATASET['folder']}/250117_TYNDP2024Scenarios_Electricity_SupplyMix.xlsx",
        log:
            "logs/retrieve_tyndp_vp_data.log",
        run:
            copy2(input["zip_file"], output["dir"] + ".zip")
            unpack_archive(output["dir"] + ".zip", output["dir"])
            os.remove(output["dir"] + ".zip")


if (NUC_PROFILES := dataset_version("tyndp_nuclear_profiles"))[
    "source"
] in ARCHIVE_SOURCES:

    rule retrieve_tyndp_nuclear_profiles:
        input:
            # TODO Derive this from Market Model Outputs directly
            zip_file=storage(NUC_PROFILES["url"]),
        output:
            dir=directory(NUC_PROFILES["folder"]),
            nuclear_p_max_pu_2030=f"{NUC_PROFILES['folder']}/nuclear_p_max_pu_2030.csv",
            nuclear_p_max_pu_2040=f"{NUC_PROFILES['folder']}/nuclear_p_max_pu_2040.csv",
        log:
            "logs/retrieve_tyndp_nuclear_profiles.log",
        run:
            copy2(input["zip_file"], output["dir"] + ".zip")
            unpack_archive(output["dir"] + ".zip", output["dir"])
            os.remove(output["dir"] + ".zip")


if (PRESOLVED_NETWORKS_DATASET := dataset_version("open_tyndp_prelim"))[
    "source"
] in ARCHIVE_SOURCES:

    rule retrieve_presolved_networks:
        input:
            zip_file=storage(PRESOLVED_NETWORKS_DATASET["url"]),
        output:
            network=f"{PRESOLVED_NETWORKS_DATASET['folder']}/base_s_all___{{planning_horizons}}.nc",
        log:
            "logs/retrieve_presolved_networks_{planning_horizons}.log",
        run:
            from pathlib import Path
            from shutil import copyfileobj
            from zipfile import ZipFile

            target_suffix = (
                f"networks/base_s_all___{wildcards.planning_horizons}.nc"
            )
            with ZipFile(input["zip_file"], "r") as zf:
                matches = [m for m in zf.namelist() if m.endswith(target_suffix)]
                if not matches:
                    raise ValueError(
                        f"Could not find '{target_suffix}' in {input['zip_file']}."
                    )
                out_path = Path(output["network"])
                out_path.parent.mkdir(parents=True, exist_ok=True)
                with zf.open(matches[0]) as src, out_path.open("wb") as dst:
                    copyfileobj(src, dst)



# Versioning not implemented as the dataset is used only for plotting
# License - MIT - Copyright (c) 2021 Gavin Rehkemper
# Website: https://github.com/gavinr/world-countries-centroids
rule retrieve_countries_centroids:
    output:
        "data/countries_centroids.geojson",
    log:
        "logs/retrieve_countries_centroids.log",
    run:
        from scripts._helpers import progress_retrieve

        progress_retrieve(
            "https://cdn.jsdelivr.net/gh/gavinr/world-countries-centroids@v1.0.0/dist/countries.geojson",
            output[0],
            disable=True,
        )


# Development
#############
if not "pre-built" in PECD_DATASET["version"]:

    def get_pecd_prebuilt_version(increment_minor=True):
        prebuilt_prefix = f"{PECD_DATASET['version']}+pre-built."
        versions = (
            dataset_version("tyndp_pecd", all_versions=True)
            .query("version.str.contains(@prebuilt_prefix, regex=False)")
            .version.sort_values()
        )

        if versions.empty:
            return "0.1"

        major, minor = versions.iloc[-1].removeprefix(prebuilt_prefix).rsplit(".", 1)

        if increment_minor:
            return f"{major}.{str(int(minor)+1)}"
        else:
            return f"{str(int(major)+1)}.0"

    rule prepare_pecd_release:
        input:
            pecd_raw=PECD_DATASET["folder"],
        output:
            pecd_prebuilt=directory(
                f"{PECD_DATASET['folder']}+pre-built.{get_pecd_prebuilt_version(increment_minor= True)}"
            ),
        log:
            "logs/prepare_pecd_release.log",
        benchmark:
            benchmarks("performances/prepare_pecd_release")
        threads: 4
        resources:
            mem_mb=1000,
        params:
            cyears=config_provider(
                "electricity", "pecd_renewable_profiles", "pre_built", "cyears"
            ),
            available_pyears=config_provider(
                "electricity", "pecd_renewable_profiles", "available_years"
            ),
        script:
            scripts("sb/prepare_pecd_release.py")


# Build electricity
###################

if config["load"]["source"] == "tyndp":

    rule clean_tyndp_electricity_demand:
        input:
            electricity_demand=rules.retrieve_tyndp.output.demand_profiles,
        output:
            electricity_demand_prepped=resources("electricity_demand_raw_tyndp.csv"),
        log:
            logs("clean_tyndp_electricity_demand.log"),
        benchmark:
            benchmarks("performances/clean_tyndp_electricity_demand")
        conda:
            "../envs/environment.yaml"
        threads: 4
        resources:
            mem_mb=4000,
        params:
            planning_horizons=config_provider("scenario", "planning_horizons"),
            snapshots=config_provider("snapshots"),
            scenario=config_provider("tyndp_scenario"),
            available_years=config_provider("load", "available_years_tyndp"),
        script:
            scripts("sb/clean_tyndp_electricity_demand.py")


use rule build_electricity_demand as build_electricity_demand_tyndp with:
    input:
        unpack(input_elec_demand),
        tyndp=rules.clean_tyndp_electricity_demand.output.electricity_demand_prepped,
    output:
        resources("electricity_demand_{planning_horizons}.csv"),
    log:
        logs("build_electricity_demand_{planning_horizons}.log"),
    benchmark:
        benchmarks("performances/build_electricity_demand_{planning_horizons}")


def get_pecd_prebuilt(w):
    if "pre-built" in PECD_DATASET["version"]:
        return rules.retrieve_tyndp_pecd.output.dir
    else:
        return rules.prepare_pecd_release.output.pecd_prebuilt


rule clean_pecd_data:
    input:
        pecd_prebuilt=get_pecd_prebuilt,
        offshore_buses=rules.retrieve_tyndp.output.offshore_nodes,
        onshore_buses=resources("busmap_base_s_all.csv"),
    output:
        pecd_data_clean=resources("pecd_data_{technology}_{planning_horizons}.csv"),
    log:
        logs("clean_pecd_data_{technology}_{planning_horizons}.log"),
    benchmark:
        benchmarks("performances/clean_pecd_data_{technology}_{planning_horizons}")
    threads: 4
    resources:
        mem_mb=4000,
    params:
        snapshots=config_provider("snapshots"),
        drop_leap_day=config_provider("enable", "drop_leap_day"),
        fill_gaps_method=config_provider(
            "electricity", "pecd_renewable_profiles", "fill_gaps_method"
        ),
        available_years=config_provider(
            "electricity", "pecd_renewable_profiles", "available_years"
        ),
        prebuilt_years=config_provider(
            "electricity", "pecd_renewable_profiles", "pre_built", "cyears"
        ),
    script:
        scripts("sb/clean_pecd_data.py")


def input_data_pecd(w):
    available_years = config_provider(
        "electricity", "pecd_renewable_profiles", "available_years"
    )(w)
    planning_horizons = config_provider("scenario", "planning_horizons")(w)
    safe_pyears = set(
        safe_pyear(year, available_years, "PECD", verbose=False)
        for year in planning_horizons
    )
    return {
        f"pecd_data_{pyear}": resources("pecd_data_{technology}_" + str(pyear) + ".csv")
        for pyear in safe_pyears
    }


rule build_renewable_profiles_pecd:
    input:
        unpack(input_data_pecd),
    output:
        profile=resources("profile_pecd_{clusters}_{technology}.nc"),
    log:
        logs("build_renewable_profile_pecd_{clusters}_{technology}.log"),
    benchmark:
        benchmarks("performances/build_renewable_profile_pecd_{clusters}_{technology}")
    wildcard_constraints:
        technology="(?!hydro).*",  # Any technology other than hydro
    threads: 1
    resources:
        mem_mb=4000,
    params:
        planning_horizons=config_provider("scenario", "planning_horizons"),
        available_years=config_provider(
            "electricity", "pecd_renewable_profiles", "available_years"
        ),
    script:
        scripts("sb/build_renewable_profiles_pecd.py")


pemmdb_techs = branch(
    config_provider("electricity", "pemmdb_capacities", "enable"),
    config_provider("electricity", "pemmdb_capacities", "technologies"),
)


rule build_pemmdb_data:
    input:
        pemmdb_dir=rules.retrieve_tyndp.output.pemmdb,
        carrier_mapping="data/tyndp_technology_map.csv",
        busmap=resources("busmap_base_s_all.csv"),
    output:
        pemmdb_capacities=resources("pemmdb_capacities_{planning_horizons}.csv"),
        pemmdb_profiles=resources("pemmdb_profiles_{planning_horizons}.nc"),
    log:
        logs("build_pemmdb_data_{planning_horizons}.log"),
    benchmark:
        benchmarks("performances/build_pemmdb_data_{planning_horizons}")
    threads: config_provider("electricity", "pemmdb_capacities", "nprocesses")
    resources:
        mem_mb=16000,
    params:
        pemmdb_techs=pemmdb_techs,
        snapshots=config_provider("snapshots"),
        drop_leap_day=config_provider("enable", "drop_leap_day"),
        available_years=config_provider(
            "electricity", "pemmdb_capacities", "available_years"
        ),
        tyndp_scenario=config_provider("tyndp_scenario"),
    script:
        scripts("sb/build_pemmdb_data.py")


def get_elec_project_build_years(w):
    return config_provider("tyndp_investment_candidates", "elec_projects")(w)[
        int(w.planning_horizons)
    ]


def get_h2_project_build_years(w):
    return config_provider("tyndp_investment_candidates", "h2_projects")(w)[
        int(w.planning_horizons)
    ]


rule build_tyndp_transmission_projects:
    input:
        buses_elec=rules.build_tyndp_network.output.substations_geojson,
        buses_h2=rules.build_tyndp_network.output.substations_h2_geojson,
        invest_grid=rules.retrieve_tyndp.output.invest_grid,
    output:
        new_links_elec=resources("tyndp/new_links_{planning_horizons}.csv"),
        new_links_h2=resources("tyndp/new_links_h2_{planning_horizons}.csv"),
    log:
        logs("build_tyndp_transmission_projects_{planning_horizons}.log"),
    benchmark:
        benchmarks("performances/build_tyndp_transmission_projects_{planning_horizons}")
    threads: 1
    resources:
        mem_mb=1000,
    params:
        build_years_elec=get_elec_project_build_years,
        build_years_h2=get_h2_project_build_years,
    script:
        scripts("sb/build_tyndp_transmission_projects.py")


rule build_tyndp_trajectories:
    input:
        trajectories=rules.retrieve_tyndp.output.trajectories,
        carrier_mapping="data/tyndp_technology_map.csv",
    output:
        tyndp_trajectories=resources("tyndp_trajectories.csv"),
    log:
        logs("build_tyndp_trajectories.log"),
    benchmark:
        benchmarks("performances/build_tyndp_trajectories")
    threads: 4
    params:
        tyndp_scenario=config_provider("tyndp_scenario"),
    script:
        scripts("sb/build_tyndp_trajectories.py")


rule clean_tyndp_hydro_inflows:
    input:
        hydro_inflows_dir=rules.retrieve_tyndp.output.hydro_inflows,
        busmap=resources("busmap_base_s_all.csv"),
    output:
        hydro_inflows_tyndp=resources(
            "hydro_inflows_tyndp_{tech}_{planning_horizons}.csv"
        ),
    log:
        logs("clean_tyndp_hydro_inflows_{tech}_{planning_horizons}.log"),
    benchmark:
        benchmarks("performances/clean_tyndp_hydro_inflows_{tech}_{planning_horizons}")
    retries: 2
    threads: 4
    params:
        snapshots=config_provider("snapshots"),
        drop_leap_day=config_provider("enable", "drop_leap_day"),
        available_years=config_provider(
            "electricity", "pemmdb_hydro_profiles", "available_years"
        ),
    script:
        scripts("sb/clean_tyndp_hydro_inflows.py")


def input_data_hydro_tyndp(w):
    available_years = config_provider(
        "electricity", "pemmdb_hydro_profiles", "available_years"
    )(w)
    planning_horizons = config_provider("scenario", "planning_horizons")(w)
    safe_pyears = set(
        safe_pyear(
            year,
            available_years,
            "PEMMDB hydro",
            verbose=False,
        )
        for year in planning_horizons
    )
    technologies = config_provider(
        "electricity", "pemmdb_hydro_profiles", "technologies"
    )(w)
    return {
        f"hydro_inflow_tyndp_{tech}_{pyear}": resources(
            f"hydro_inflows_tyndp_{tech}_{str(pyear)}.csv"
        )
        for pyear in safe_pyears
        for tech in technologies
    }


rule build_tyndp_hydro_profile:
    input:
        unpack(input_data_hydro_tyndp),
        carrier_mapping="data/tyndp_technology_map.csv",
    output:
        profile=resources("profile_pemmdb_hydro.nc"),
    log:
        logs("build_tyndp_hydro_profile.log"),
    benchmark:
        benchmarks("performances/build_tyndp_hydro_profile")
    resources:
        mem_mb=5000,
    params:
        snapshots=config_provider("snapshots"),
        drop_leap_day=config_provider("enable", "drop_leap_day"),
        planning_horizons=config_provider("scenario", "planning_horizons"),
        available_years=config_provider(
            "electricity", "pemmdb_hydro_profiles", "available_years"
        ),
        technologies=config_provider(
            "electricity", "pemmdb_hydro_profiles", "technologies"
        ),
    script:
        scripts("sb/build_tyndp_hydro_profile.py")


use rule build_electricity_demand_base as build_electricity_demand_base_tyndp with:
    input:
        unpack(input_elec_demand_base),
        raster=[],
        gb_excel=[],
        gb_geojson=[],
        nuts3=[],
        load=resources("electricity_demand_{planning_horizons}.csv"),
    output:
        resources("electricity_demand_base_s_{planning_horizons}.nc"),
    log:
        logs("build_electricity_demand_base_s_{planning_horizons}.log"),
    benchmark:
        benchmarks("performances/build_electricity_demand_base_s_{planning_horizons}")


# Build sector
##############


rule build_tyndp_gas_demand:
    input:
        supply_tool=rules.retrieve_tyndp.output.supply_tool,
    output:
        gas_demand=resources("gas_demand_tyndp_{planning_horizons}.csv"),
    log:
        logs("build_tyndp_gas_demand_{planning_horizons}.log"),
    benchmark:
        benchmarks("performances/build_tyndp_gas_demand_{planning_horizons}")
    conda:
        "../envs/environment.yaml"
    threads: 1
    resources:
        mem_mb=1000,
    params:
        scenario=config_provider("tyndp_scenario"),
        planning_horizons=config_provider("scenario", "planning_horizons"),
    script:
        scripts("sb/build_tyndp_gas_demand.py")


rule build_tyndp_h2_demand:
    input:
        h2_demand=rules.retrieve_tyndp.output.demand_profiles,
    output:
        h2_demand=resources("h2_demand_tyndp_{planning_horizons}.csv"),
    log:
        logs("build_tyndp_h2_demand_{planning_horizons}.log"),
    benchmark:
        benchmarks("performances/build_tyndp_h2_demand_{planning_horizons}")
    threads: 1
    resources:
        mem_mb=1000,
    params:
        snapshots=config_provider("snapshots"),
        drop_leap_day=config_provider("enable", "drop_leap_day"),
        scenario=config_provider("tyndp_scenario"),
    script:
        scripts("sb/build_tyndp_h2_demand.py")


if config["sector"]["h2_topology_tyndp"]:

    def include_tyndp_h2_projects(w):
        horizons = config_provider("tyndp_investment_candidates", "h2_projects")(w)
        if not horizons:
            return False
        return int(w.planning_horizons) in horizons

    rule build_tyndp_h2_network:
        input:
            h2_reference_grid_entsoe=rules.retrieve_tyndp.output.h2_reference_grid_entsoe,
            h2_reference_grid_entsos=rules.retrieve_tyndp.output.h2_reference_grid_entsos,
            h2_projects=branch(
                include_tyndp_h2_projects,
                resources("tyndp/new_links_h2_{planning_horizons}.csv"),
            ),
        output:
            h2_grid_prepped=resources("h2_reference_grid_tyndp_{planning_horizons}.csv"),
            interzonal_prepped=resources("h2_interzonal_tyndp_{planning_horizons}.csv"),
        log:
            logs("build_tyndp_h2_network_{planning_horizons}.log"),
        benchmark:
            benchmarks("performances/build_tyndp_h2_network_{planning_horizons}")
        conda:
            "../envs/environment.yaml"
        threads: 1
        resources:
            mem_mb=4000,
        params:
            snapshots=config_provider("snapshots"),
            scenario=config_provider("tyndp_scenario"),
            h2_reference_grid_source=config_provider(
                "sector", "h2_reference_grid_source"
            ),
        script:
            scripts("sb/build_tyndp_h2_network.py")

    rule clean_tyndp_h2_imports:
        input:
            import_potentials_raw=rules.retrieve_tyndp.output.h2_imports,
            countries_centroids=rules.retrieve_countries_centroids.output,
        output:
            import_potentials_prepped=resources("h2_import_potentials_prepped.csv"),
        log:
            logs("clean_tyndp_h2_imports.log"),
        benchmark:
            benchmarks("performances/clean_tyndp_h2_imports")
        conda:
            "../envs/environment.yaml"
        threads: 1
        resources:
            mem_mb=4000,
        script:
            scripts("sb/clean_tyndp_h2_imports.py")

    rule build_tyndp_h2_imports:
        input:
            import_potentials_prepped=rules.clean_tyndp_h2_imports.output.import_potentials_prepped,
        output:
            import_potentials_filtered=resources(
                "h2_import_potentials_{planning_horizons}.csv"
            ),
        log:
            logs("build_tyndp_h2_imports_{planning_horizons}.log"),
        benchmark:
            benchmarks("performances/build_tyndp_h2_imports_{planning_horizons}")
        conda:
            "../envs/environment.yaml"
        threads: 1
        resources:
            mem_mb=4000,
        params:
            scenario=config_provider("tyndp_scenario"),
        script:
            scripts("sb/build_tyndp_h2_imports.py")

    rule clean_tyndp_smr:
        input:
            smr=rules.retrieve_tyndp.output.smr,
        output:
            smr_prepped=resources("smr_data_prepped_{planning_horizons}.csv"),
        log:
            logs("clean_tyndp_smr_{planning_horizons}.log"),
        benchmark:
            benchmarks("performances/clean_tyndp_smr_{planning_horizons}")
        conda:
            "../envs/environment.yaml"
        threads: 1
        resources:
            mem_mb=4000,
        params:
            tyndp_scenario=config_provider("tyndp_scenario"),
            h2_zones_tyndp=config_provider("sector", "h2_zones_tyndp"),
        script:
            scripts("sb/clean_tyndp_smr.py")

    rule clean_tyndp_h2_storages:
        input:
            h2_storages=rules.retrieve_tyndp.output.h2_storages,
        output:
            h2_storages_prepped=resources("h2_storages_prepped_{planning_horizons}.csv"),
        log:
            logs("clean_tyndp_h2_storages_{planning_horizons}.log"),
        benchmark:
            benchmarks("performances/clean_tyndp_h2_storages_{planning_horizons}")
        conda:
            "../envs/environment.yaml"
        threads: 1
        resources:
            mem_mb=4000,
        params:
            tyndp_scenario=config_provider("tyndp_scenario"),
            h2_zones_tyndp=config_provider("sector", "h2_zones_tyndp"),
        script:
            scripts("sb/clean_tyndp_h2_storages.py")


if config["sector"]["offshore_hubs_tyndp"]["enable"]:

    rule build_tyndp_offshore_hubs:
        input:
            nodes=rules.retrieve_tyndp.output.offshore_nodes,
            grid=rules.retrieve_tyndp.output.offshore_grid,
            electrolysers=rules.retrieve_tyndp.output.offshore_electrolysers,
            generators=rules.retrieve_tyndp.output.offshore_generators,
        output:
            offshore_buses=resources("offshore_buses.csv"),
            offshore_grid=resources("offshore_grid.csv"),
            offshore_electrolysers=resources("offshore_electrolysers.csv"),
            offshore_generators=resources("offshore_generators.csv"),
            offshore_zone_trajectories=resources("offshore_zone_trajectories.csv"),
        log:
            logs("build_tyndp_offshore_hubs.log"),
        benchmark:
            benchmarks("performances/build_tyndp_offshore_hubs")
        conda:
            "../envs/environment.yaml"
        threads: 1
        resources:
            mem_mb=4000,
        params:
            planning_horizons=config_provider("scenario", "planning_horizons"),
            scenario=config_provider("tyndp_scenario"),
            countries=config_provider("countries"),
            offshore_hubs_tyndp=config_provider("sector", "offshore_hubs_tyndp"),
            extendable_carriers=config_provider("electricity", "extendable_carriers"),
            h2_zones_tyndp=config_provider("sector", "h2_zones_tyndp"),
        script:
            scripts("sb/build_tyndp_offshore_hubs.py")


rule group_tyndp_conventionals:
    input:
        pemmdb_capacities=resources("pemmdb_capacities_{planning_horizon}.csv"),
        pemmdb_profiles=resources("pemmdb_profiles_{planning_horizon}.nc"),
        carrier_mapping="data/tyndp_technology_map.csv",
    output:
        pemmdb_capacities_grouped=resources(
            "pemmdb_capacities_{planning_horizon}_grouped.csv"
        ),
        pemmdb_profiles_grouped=resources(
            "pemmdb_profiles_{planning_horizon}_grouped.nc"
        ),
    log:
        logs("group_tyndp_conventionals_{planning_horizon}.log"),
    benchmark:
        benchmarks("performances/group_tyndp_conventionals_{planning_horizon}")
    conda:
        "../envs/environment.yaml"
    threads: 1
    resources:
        mem_mb=2000,
    params:
        tyndp_conventional_carriers=config_provider(
            "electricity", "tyndp_conventional_carriers"
        ),
    script:
        scripts("sb/group_tyndp_conventionals.py")


# Postprocess
#############

if config["foresight"] != "perfect":

    rule plot_base_hydrogen_network:
        input:
            network=resources(
                "networks/base_s_{clusters}_{opts}_{sector_opts}_{planning_horizons}.nc"
            ),
            regions_onshore=resources("regions_onshore.geojson"),
        output:
            map=resources(
                "maps/base_h2_network_{clusters}_{opts}_{sector_opts}_{planning_horizons}.pdf"
            ),
        log:
            RESULTS
            + "logs/plot_base_hydrogen_network_{clusters}_{opts}_{sector_opts}_{planning_horizons}.log",
        benchmark:
            benchmarks(
                "performances/plot_base_hydrogen_network_{clusters}_{opts}_{sector_opts}_{planning_horizons}"
            )
        conda:
            "../envs/environment.yaml"
        threads: 1
        resources:
            mem_mb=4000,
        params:
            plotting=config_provider("plotting"),
        script:
            scripts("sb/plot_base_hydrogen_network.py")

    rule plot_base_offshore_network:
        input:
            network=resources(
                "networks/base_s_{clusters}_{opts}_{sector_opts}_{planning_horizons}.nc"
            ),
            regions_offshore=resources("regions_offshore.geojson"),
        output:
            map=resources(
                "maps/base_offshore_network_{clusters}_{opts}_{sector_opts}_{planning_horizons}_{carrier}.pdf"
            ),
        log:
            RESULTS
            + "logs/plot_base_offshore_network_{clusters}_{opts}_{sector_opts}_{planning_horizons}_{carrier}.log",
        benchmark:
            benchmarks(
                "performances/plot_base_offshore_network_{clusters}_{opts}_{sector_opts}_{planning_horizons}_{carrier}"
            )
        conda:
            "../envs/environment.yaml"
        threads: 1
        resources:
            mem_mb=4000,
        params:
            plotting=config_provider("plotting"),
            expanded=False,
        script:
            scripts("sb/plot_offshore_network.py")

    use rule plot_base_offshore_network as plot_offshore_network with:
        input:
            network=RESULTS
            + "networks/base_s_{clusters}_{opts}_{sector_opts}_{planning_horizons}.nc",
        output:
            map=RESULTS
            + "maps/base_s_{clusters}_{opts}_{sector_opts}_{planning_horizons}-offshore_network_{carrier}.pdf",
        log:
            RESULTS
            + "logs/plot_offshore_network_{clusters}_{opts}_{sector_opts}_{planning_horizons}_{carrier}.log",
        benchmark:
            benchmarks(
                "performances/plot_offshore_network_{clusters}_{opts}_{sector_opts}_{planning_horizons}_{carrier}"
            )
        params:
            expanded=True,


# Benchmark
###########

if config["benchmarking"]["enable"]:

    rule clean_tyndp_output_benchmark:
        input:
            # TODO Generalize hardcoded climate year CY2009 for DE / GA
            tyndp_output_file=lambda w: getattr(
                rules.retrieve_tyndp.output,
                f"market_outputs_{w.scenario}{w.planning_horizons}_CY2009",
            ),
            carrier_mapping="data/tyndp_technology_map.csv",
        output:
            benchmarks=RESULTS
            + "benchmarks/tyndp-2024/resources/benchmarks_tyndp_output_{scenario}{planning_horizons}.csv",
            crossborder=RESULTS
            + "benchmarks/tyndp-2024/resources/benchmarks_tyndp_output_crossborder_{scenario}{planning_horizons}.csv",
            h2_demand=RESULTS
            + "benchmarks/tyndp-2024/resources/benchmarks_tyndp_output_h2_demand_{scenario}{planning_horizons}.csv",
            elec_demand=RESULTS
            + "benchmarks/tyndp-2024/resources/benchmarks_tyndp_output_elec_demand_{scenario}{planning_horizons}.csv",
        log:
            logs("clean_tyndp_output_benchmark_{scenario}{planning_horizons}.log"),
        benchmark:
            benchmarks(
                "performances/clean_tyndp_output_benchmark_{scenario}{planning_horizons}"
            )
        wildcard_constraints:
            planning_horizons="(2030|2040)",  # Only years with MM output data
        threads: 4
        resources:
            mem_mb=8000,
        params:
            benchmarking=config_provider("benchmarking"),
            scenario=config_provider("tyndp_scenario"),
            snapshots=config_provider("snapshots"),
            drop_leap_day=config_provider("enable", "drop_leap_day"),
            countries=config_provider("countries"),
            offshore_hubs=config_provider("sector", "offshore_hubs_tyndp", "enable"),
        script:
            scripts("sb/clean_tyndp_output_benchmark.py")

    rule clean_tyndp_report_benchmark:
        input:
            scenarios_figures=rules.retrieve_tyndp.output.benchmark,
            carrier_mapping="data/tyndp_technology_map.csv",
        output:
            benchmarks=RESULTS + "benchmarks/tyndp-2024/resources/benchmarks_tyndp.csv",
        log:
            logs("clean_tyndp_report_benchmark.log"),
        benchmark:
            benchmarks("performances/clean_tyndp_report_benchmark")
        threads: 4
        resources:
            mem_mb=8000,
        params:
            benchmarking=config_provider("benchmarking"),
            scenario=config_provider("tyndp_scenario"),
            snapshots=config_provider("snapshots"),
        script:
            scripts("sb/clean_tyndp_report_benchmark.py")

    rule clean_tyndp_vp_data:
        input:
            elec_demand=rules.retrieve_tyndp_vp_data.output.elec_demand,
            elec_supplymix=rules.retrieve_tyndp_vp_data.output.elec_supply,
            elec_flex=rules.retrieve_tyndp_vp_data.output.elec_flex,
            carrier_mapping="data/tyndp_technology_map.csv",
        output:
            RESULTS + "benchmarks/tyndp-2024/resources/vp_data_tyndp.csv",
        log:
            logs("clean_tyndp_vp_data.log"),
        benchmark:
            benchmarks("performances/clean_tyndp_vp_data")
        threads: 4
        resources:
            mem_mb=8000,
        params:
            scenario=config_provider("tyndp_scenario"),
            snapshots=config_provider("snapshots"),
            unit_conversion=config_provider("benchmarking", "unit_conversion"),
        script:
            scripts("sb/clean_tyndp_vp_data.py")

    rule build_statistics:
        input:
            network=RESULTS
            + "networks/base_s_{clusters}_{opts}_{sector_opts}_{planning_horizons}.nc",
            carrier_mapping="data/tyndp_technology_map.csv",
        output:
            RESULTS
            + "benchmarks/tyndp-2024/resources/benchmarks_s_{clusters}_{opts}_{sector_opts}_{planning_horizons}.csv",
        log:
            python=logs(
                "build_statistics_s_{clusters}_{opts}_{sector_opts}_{planning_horizons}.log"
            ),
        benchmark:
            benchmarks(
                "performances/build_statistics_s_{clusters}_{opts}_{sector_opts}_{planning_horizons}"
            )
        threads: 1
        resources:
            mem_mb=8000,
        params:
            benchmarking=config_provider("benchmarking"),
            scenario=config_provider("tyndp_scenario"),
            tyndp_renewable_carriers=config_provider(
                "electricity", "tyndp_renewable_carriers"
            ),
            load_shedding=config_provider(
                "solving", "options", "load_shedding", "carriers"
            ),
            low_voltage=config_provider("sector", "electricity_distribution_grid"),
            group_tyndp_conventionals=config_provider(
                "electricity", "group_tyndp_conventionals"
            ),
        script:
            scripts("sb/build_statistics.py")

    rule make_benchmark:
        input:
            results=expand(
                RESULTS
                + "benchmarks/tyndp-2024/resources/benchmarks_s_{clusters}_{opts}_{sector_opts}_{planning_horizons}.csv",
                planning_horizons=config_provider("scenario", "planning_horizons"),
                allow_missing=True,
            ),
            benchmarks=RESULTS + "benchmarks/tyndp-2024/resources/benchmarks_tyndp.csv",
            mm_data=lambda w: (
                expand(
                    RESULTS
                    + "benchmarks/tyndp-2024/resources/benchmarks_tyndp_output_{scenario}{planning_horizons}.csv",
                    scenario=config_provider("tyndp_scenario"),
                    planning_horizons=[
                        year
                        for year in config_provider("scenario", "planning_horizons")(w)
                        if str(year)
                        in ["2030", "2040"]  # Only years with MM output data
                    ],
                    allow_missing=True,
                )
                if config_provider("tyndp_scenario")(w)
                == "NT"  # Only NT has MM output files for now
                else []
            ),
        output:
            benchmarks=directory(
                RESULTS
                + "benchmarks/tyndp-2024/csvs_s_{clusters}_{opts}_{sector_opts}_all_years/"
            ),
            kpis_by_bus=RESULTS
            + "benchmarks/tyndp-2024/kpis_s_{clusters}_{opts}_{sector_opts}_all_years_by_bus.csv",
            kpis_by_country=RESULTS
            + "benchmarks/tyndp-2024/kpis_s_{clusters}_{opts}_{sector_opts}_all_years_by_country.csv",
        log:
            logs("make_benchmark_s_{clusters}_{opts}_{sector_opts}_all_years.log"),
        benchmark:
            benchmarks(
                "performances/make_benchmark_s_{clusters}_{opts}_{sector_opts}_all_years"
            )
        threads: 4
        resources:
            mem_mb=8000,
        params:
            benchmarking=config_provider("benchmarking"),
            scenario=config_provider("tyndp_scenario"),
            snapshots=config_provider("snapshots"),
        script:
            scripts("sb/make_benchmark.py")

    rule plot_benchmark:
        input:
            results=expand(
                RESULTS
                + "benchmarks/tyndp-2024/resources/benchmarks_s_{clusters}_{opts}_{sector_opts}_{planning_horizons}.csv",
                planning_horizons=config_provider("scenario", "planning_horizons"),
                allow_missing=True,
            ),
            mm_data=lambda w: (
                expand(
                    RESULTS
                    + "benchmarks/tyndp-2024/resources/benchmarks_tyndp_output_{scenario}{planning_horizons}.csv",
                    scenario=config_provider("tyndp_scenario"),
                    planning_horizons=[
                        year
                        for year in config_provider("scenario", "planning_horizons")(w)
                        if str(year)
                        in ["2030", "2040"]  # Only years with MM output data
                    ],
                    allow_missing=True,
                )
                if config_provider("tyndp_scenario")(w)
                == "NT"  # Only NT has MM output files for now
                else []
            ),
            benchmarks=RESULTS + "benchmarks/tyndp-2024/resources/benchmarks_tyndp.csv",
            vp_data=RESULTS + "benchmarks/tyndp-2024/resources/vp_data_tyndp.csv",
            kpis_by_bus=RESULTS
            + "benchmarks/tyndp-2024/kpis_s_{clusters}_{opts}_{sector_opts}_all_years_by_bus.csv",
            kpis_by_country=RESULTS
            + "benchmarks/tyndp-2024/kpis_s_{clusters}_{opts}_{sector_opts}_all_years_by_country.csv",
        output:
            dir=directory(
                RESULTS
                + "benchmarks/tyndp-2024/graphics_s_{clusters}_{opts}_{sector_opts}_all_years/"
            ),
            kpis_by_bus=RESULTS
            + "benchmarks/tyndp-2024/kpis_s_{clusters}_{opts}_{sector_opts}_all_years_by_bus.pdf",
            kpis_by_country=RESULTS
            + "benchmarks/tyndp-2024/kpis_s_{clusters}_{opts}_{sector_opts}_all_years_by_country.pdf",
        log:
            logs("plot_benchmark_s_{clusters}_{opts}_{sector_opts}_all_years.log"),
        benchmark:
            benchmarks(
                "performances/plot_benchmark_s_{clusters}_{opts}_{sector_opts}_all_years"
            )
        threads: 4
        resources:
            mem_mb=8000,
        params:
            benchmarking=config_provider("benchmarking"),
            scenario=config_provider("tyndp_scenario"),
            snapshots=config_provider("snapshots"),
            tech_colors=config_provider("plotting", "tech_colors"),
            bench_colors=config_provider("plotting", "benchmarking", "colors"),
        script:
            scripts("sb/plot_benchmark.py")


# Collect
#########


rule clean_pecd_datas:
    input:
        lambda w: expand(
            resources("pecd_data_{technology}_{planning_horizons}.csv"),
            **config["scenario"],
            run=config["run"]["name"],
            technology=config_provider(
                "electricity", "pecd_renewable_profiles", "technologies"
            )(w),
        ),


rule build_renewable_profiles_pecds:
    input:
        lambda w: expand(
            resources("profile_pecd_{clusters}_{technology}.nc"),
            **config["scenario"],
            run=config["run"]["name"],
            technology=config_provider(
                "electricity", "pecd_renewable_profiles", "technologies"
            )(w),
        ),


rule prepare_benchmarks:
    input:
        expand(
            RESULTS
            + "benchmarks/tyndp-2024/resources/benchmarks_s_{clusters}_{opts}_{sector_opts}_{planning_horizons}.csv",
            **config["scenario"],
            run=config["run"]["name"],
        ),
        expand(
            RESULTS + "benchmarks/tyndp-2024/resources/benchmarks_tyndp.csv",
            run=config["run"]["name"],
        ),
        expand(
            RESULTS + "benchmarks/tyndp-2024/resources/vp_data_tyndp.csv",
            run=config["run"]["name"],
        ),


rule make_benchmarks:
    input:
        kpis_by_bus=expand(
            rules.make_benchmark.output.kpis_by_bus,
            **config["scenario"],
            run=config["run"]["name"],
        ),
        kpis_by_country=expand(
            rules.make_benchmark.output.kpis_by_country,
            **config["scenario"],
            run=config["run"]["name"],
        ),


rule plot_benchmarks:
    input:
        kpis_by_bus=expand(
            rules.plot_benchmark.output.kpis_by_bus,
            **config["scenario"],
            run=config["run"]["name"],
        ),
        kpis_by_country=expand(
            rules.plot_benchmark.output.kpis_by_country,
            **config["scenario"],
            run=config["run"]["name"],
        ),


def input_pemmdb_datas(w):
    available_years = config_provider(
        "electricity", "pemmdb_capacities", "available_years"
    )(w)
    return list(
        {
            safe_pyear(year, available_years, verbose=False)
            for year in config_provider("scenario", "planning_horizons")(w)
        }
    )


rule build_pemmdb_and_trajectories:
    input:
        expand(
            rules.build_pemmdb_data.output.pemmdb_capacities,
            planning_horizons=input_pemmdb_datas,
            run=config["run"]["name"],
        ),
        expand(
            resources("tyndp_trajectories.csv"),
            run=config["run"]["name"],
        ),


rule build_tyndp_h2_demands:
    input:
        expand(
            resources("h2_demand_tyndp_{planning_horizons}.csv"),
            **config["scenario"],
            run=config["run"]["name"],
        ),


rule build_tyndp_gas_demands:
    input:
        expand(
            resources("gas_demand_tyndp_{planning_horizons}.csv"),
            **config["scenario"],
            run=config["run"]["name"],
        ),


# Explore
###########


rule launch_explorer:
    input:
        expand(
            RESULTS
            + "networks/base_s_{clusters}_{opts}_{sector_opts}_{planning_horizons}.nc",
            run=config["run"]["name"],
            **config["scenario"],
        ),
    output:
        RESULTS + "logs/explorer_launched.log",
    params:
        port=find_free_port(start_port=8050, max_attempts=50),
        launch_msg="Launching PyPSA-Explorer...",
    run:
        import platform
        import subprocess
        import sys

        output_log = str(output[0])
        input_files = list(input)

        # Define command line executable
        cmd = [
            sys.executable,
            "scripts/sb/launch_explorer.py",
            output_log,
            str(params.port),
        ] + input_files

        print(params.launch_msg)

        # Open logfile before Popen so the log exists when the subprocess validates its path
        popen_kwargs = {
            "stdout": open(output_log, "w"),
            "stderr": subprocess.STDOUT,
        }

        # Use creationflags for Windows and start_new_session for Linux/Unix
        if platform.system() == "Windows":
            popen_kwargs["creationflags"] = subprocess.CREATE_NEW_PROCESS_GROUP
        else:
            popen_kwargs["start_new_session"] = True

        process = subprocess.Popen(cmd, **popen_kwargs)

        print(f"Explorer subprocess started with PID: {process.pid}")
        print(f"PyPSA-Explorer is running at http://127.0.0.1:{params.port}.")
        print(
            f"Your browser should open automatically. If not, click the link above."
        )



rule close_explorers:
    run:
        import psutil

        print("Closing all explorer instances...")
        killed_count = 0

        for proc in psutil.process_iter(["pid", "name", "cmdline"]):
            try:
                cmdline = proc.info.get("cmdline", [])
                if cmdline and "launch_explorer.py" in " ".join(cmdline):
                    proc.kill()
                    print(f"Killed explorer process (PID: {proc.info['pid']})")
                    killed_count += 1
            except (psutil.NoSuchProcess, psutil.AccessDenied):
                pass

        if killed_count == 0:
            print("No explorer processes found running.")
        else:
            print(f"Closed {killed_count} explorer instance(s).")


if (PRESOLVED_NETWORKS_DATASET := dataset_version("open_tyndp_prelim"))[
    "source"
] in ARCHIVE_SOURCES:

    use rule launch_explorer as launch_presolved_explorer with:
        input:
            expand(
                f"{PRESOLVED_NETWORKS_DATASET['folder']}/base_s_all___{{planning_horizons}}.nc",
                planning_horizons=config["scenario"]["planning_horizons"],
            ),
        output:
            "logs/presolved_explorer_launched.log",
        params:
            launch_msg=f"Launching PyPSA-Explorer with presolved networks for release v{PRESOLVED_NETWORKS_DATASET['version']}...",
